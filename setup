#!/bin/zsh

DOTFILE_PATH=~/dotfiles
BACKUP_PATH=~/dotfiles_old
NVIM_PATH=~/.config/nvim
ZSHINIT_PATH=~/.zshinit

DIRECTORIES_TO_CREATE=(
  $BACKUP_PATH
  $NVIM_PATH
  $ZSHINIT_PATH
)

echo "Creating necessary directories if they don't exist already"
for dir in $DIRECTORIES_TO_CREATE; do
  if [ ! -d "$dir" ]; then
    mkdir -p $dir
    echo "Created $dir"
  fi
done

# Map files under config directory to their symlink target path
typeset -A FILE_PATHS
FILE_PATHS=(
  'zsh/zshrc' '.zshrc'
  'vim/vimrc' '.vimrc'
  'nvim/vimrc' '.config/nvim/init.vim'
  'zsh/nvm_init.sh' '.zshinit/nvm_init.sh'
  'ignore' '.ignore'
)

for file_path target_path in ${(kv)FILE_PATHS}; do
  source_path="$DOTFILE_PATH/config/$file_path"
  echo "Setting up $source_path"

  # Check if file already exists and is not a symlink and move to dotfiles_old
  if [ -e ~/"$target_path" ] && [ ! -h ~/"$target_path" ]; then
    echo "Moving existing file $target_path to backup directory"
    mv ~/$target_path ~/$BACKUP_PATH/
  fi

  # Check if symlink exists for target file
  if [ -h ~/"$target_path" ]; then
    echo "Removing existing symlink..."
    rm ~/"$target_path"
  fi

  echo "Linking $source_path -> $HOME/$target_path"
  ln -s $DOTFILE_PATH/config/$file_path ~/$target_path
done
